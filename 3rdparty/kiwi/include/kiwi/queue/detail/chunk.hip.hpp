/**
 * @file chunk.hip.hpp
 * @brief Device-to-host wire format and device push API
 *
 * Ring layout. A device-to-host ring is `capacity` 64-byte lines of four
 * 16-byte chunks. Lane c of the pushing wavefront writes chunk c of a message
 * with one 16-byte store (global_store_dwordx4):
 *
 *   line = 64 B   +-------------+-------------+-------------+-------------+
 *                 |   chunk 0   |   chunk 1   |   chunk 2   |   chunk 3   |
 *                 +------+------+------+------+------+------+------+------+
 *                 | data | sig  | data | sig  | data | sig  | data | sig  |
 *                 |  8 B |  8 B |  8 B |  8 B |  8 B |  8 B |  8 B |  8 B |
 *                 +------+------+------+------+------+------+------+------+
 *
 *   data: payload bytes [8c, 8c + 8) of the message. Chunks wholly past the
 *         payload carry zero; the bytes of the last word past the payload
 *         are unspecified (push_shared/push_global/push_lanes pass them
 *         through) and never returned by pop().
 *   sig:  bits  0-7   epoch = (line index >> log2(capacity)) & 0xff, in every
 *                     chunk, including the padding chunks of the last line
 *         bits  8-15  reserved, zero
 *         bits 16-31  payload length in bytes  (chunk 0 only)
 *         bits 32-63  32-bit metadata          (chunk 0 only), e.g. the
 *                     invoke callback id
 *
 * A message of B bytes uses message_chunks(B) = max(1, ceil(B / 8)) chunks,
 * at most one per lane, so B <= kMaxMessageBytes = 512. It starts on a line
 * and occupies message_lines(B) whole lines; a message that reaches the end
 * of the ring continues at line 0 of the next lap.
 *
 * The host consumes a message only when every chunk of its lines, padding
 * included, carries the lap epoch of its own line, so a partly visible
 * message is never consumed. This relies on 16-byte GPU stores to host memory and
 * aligned 16-byte host loads being single-copy atomic (x86-64 consumer).
 * Producers never write line i + capacity before the consumer has published
 * a position past line i, so a line holds either the expected lap or the one
 * before it, and an 8-bit epoch tells them apart. Empty lines start as lap -1
 * (epoch 0xff).
 */

#pragma once

#include "address_space.hpp"
#include "../error.hpp"

#include <hip/hip_runtime.h>
#include <cstddef>
#include <cstdint>
#include <type_traits>

// Every device-to-host push is wavefront-collective over 64 lanes (one chunk
// per lane). GFX9 (CDNA: gfx90a, gfx942, gfx950) runs only wave64.
#if defined(__HIP_DEVICE_COMPILE__) && !defined(__GFX9__)
#error "KIWI device-to-host queues require a wave64 GFX9 target"
#endif

namespace kiwi::queue {

/// Largest device-to-host message: one 8-byte payload word per lane.
inline constexpr size_t kMaxMessageBytes = 512;

/// Ring lines a kMaxMessageBytes message occupies. A ring must hold at least
/// message_lines(B) lines for the largest message B it carries.
inline constexpr size_t kMaxMessageLines = 16;

namespace detail {

/// One 16-byte chunk: [0] payload word, [1] signal word. A vector type, so a
/// chunk is moved with one 16-byte access.
using Chunk = uint64_t __attribute__((ext_vector_type(2)));

inline constexpr size_t kChunkPayloadBytes = 8;
inline constexpr size_t kChunksPerLine = 4;
inline constexpr size_t kLineBytes = kChunksPerLine * sizeof(Chunk);

/// Chunks a message of `bytes` payload bytes uses (a header-only message
/// still uses one).
__host__ __device__ constexpr size_t message_chunks(size_t bytes) {
    return bytes == 0 ? 1 : (bytes + kChunkPayloadBytes - 1) / kChunkPayloadBytes;
}

} // namespace detail

/// Ring lines a message of `bytes` payload bytes occupies.
__host__ __device__ constexpr size_t message_lines(size_t bytes) {
    return (detail::message_chunks(bytes) + detail::kChunksPerLine - 1) /
           detail::kChunksPerLine;
}

static_assert(message_lines(kMaxMessageBytes) == kMaxMessageLines);

namespace detail {

/// Signal word of a chunk; `bytes` and `metadata` are nonzero only in chunk 0.
__host__ __device__ constexpr uint64_t make_signal(uint8_t epoch, uint32_t bytes,
                                                   uint32_t metadata) {
    return static_cast<uint64_t>(epoch)
         | (static_cast<uint64_t>(bytes & 0xffffu) << 16)
         | (static_cast<uint64_t>(metadata) << 32);
}
__host__ __device__ constexpr uint8_t signal_epoch(uint64_t signal) {
    return static_cast<uint8_t>(signal);
}
__host__ __device__ constexpr uint32_t signal_bytes(uint64_t signal) {
    return static_cast<uint32_t>(signal >> 16) & 0xffffu;
}
__host__ __device__ constexpr uint32_t signal_metadata(uint64_t signal) {
    return static_cast<uint32_t>(signal >> 32);
}

/// Lap epoch of absolute line index `line`.
__host__ __device__ constexpr uint8_t line_epoch(uint64_t line, uint32_t log2_capacity) {
    return static_cast<uint8_t>(line >> log2_capacity);
}

/// Lane 0's value in every lane, kept wave-uniform (v_readfirstlane_b32, so
/// the result lives in SGPRs). The whole wavefront must be active.
__device__ __forceinline__ uint64_t broadcast_lane0(uint64_t value) {
    const uint32_t lo = __builtin_amdgcn_readfirstlane(static_cast<uint32_t>(value));
    const uint32_t hi = __builtin_amdgcn_readfirstlane(static_cast<uint32_t>(value >> 32));
    return (static_cast<uint64_t>(hi) << 32) | lo;
}

/// Published consumer index, re-read from host memory on every call.
__device__ __forceinline__ uint64_t load_published(global_ptr<const uint64_t> index) {
    return __hip_atomic_load(index, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_SYSTEM);
}

/// Payload word `__lane_id()` of `message`. Every word is read at a constant
/// offset and picked with selects, so a message held in registers stays there
/// (no dynamically indexed private array, no scratch).
template<typename T>
__device__ __forceinline__ uint64_t select_lane_word(const T& message) {
    constexpr size_t bytes = sizeof(T);
    const unsigned char* src = reinterpret_cast<const unsigned char*>(&message);
    const size_t lane = __lane_id();
    uint64_t selected = 0;
#pragma unroll
    for (size_t k = 0; k < message_chunks(bytes); ++k) {
        uint64_t word = 0;
        if (k < bytes / kChunkPayloadBytes) {
            __builtin_memcpy(&word, src + k * kChunkPayloadBytes, kChunkPayloadBytes);
        } else {
            __builtin_memcpy(&word, src + k * kChunkPayloadBytes, bytes % kChunkPayloadBytes);
        }
        selected = lane == k ? word : selected;
    }
    return selected;
}

/// Lane c stores chunk c of a `bytes`-byte message of `lines` lines starting
/// at absolute line `base`; lanes past the payload write zero data, lanes past
/// the last line write nothing.
__device__ __forceinline__ void write_message(global_ptr<Chunk> ring, uint32_t log2_capacity,
                                              uint64_t base, uint64_t word, uint32_t bytes,
                                              uint32_t metadata, uint64_t lines) {
    const size_t lane = __lane_id();
    if (lane < lines * kChunksPerLine) {
        const uint64_t line = base + lane / kChunksPerLine;
        const uint8_t epoch = line_epoch(line, log2_capacity);
        const uint64_t data = lane * kChunkPayloadBytes < bytes ? word : 0;
        const uint64_t signal = lane == 0 ? make_signal(epoch, bytes, metadata) : epoch;
        const uint64_t mask = (uint64_t{1} << log2_capacity) - 1;
        ring[(line & mask) * kChunksPerLine + lane % kChunksPerLine] = Chunk{data, signal};
    }
}

/// Debug builds trap unless all 64 lanes call the push (the wavefront
/// contract); release builds check nothing.
__device__ __forceinline__ void assert_full_wave() {
    KIWI_QUEUE_ASSERT(__builtin_amdgcn_read_exec() == ~uint64_t{0});
}

/// Wave-uniform message length; traps on a message that can never be framed.
__device__ __forceinline__ uint32_t checked_bytes(size_t bytes) {
    if (bytes > kMaxMessageBytes) {
        __builtin_trap();
    }
    return static_cast<uint32_t>(bytes);
}

/**
 * @brief Payload-source front ends of a device-to-host queue handle
 *
 * `Handle` provides push_lanes(word, bytes, metadata), the core push. All
 * device operations are wavefront-collective: all 64 lanes call them with
 * identical arguments.
 */
template<typename Handle>
struct PushApi {
    /**
     * @brief Push `message` (sizeof(T) bytes) held in registers
     *
     * Lane c selects payload word c with compile-time offsets, so the
     * message is never placed in scratch. The whole message lives in every
     * lane's registers; prefer push_shared() for large messages built in LDS.
     */
    template<typename T>
    __device__ bool push_reg(const T& message, uint32_t metadata = 0) {
        static_assert(std::is_trivially_copyable_v<T>, "push_reg() copies the message bytes");
        static_assert(sizeof(T) <= kMaxMessageBytes, "message exceeds kMaxMessageBytes");
        return handle().push_lanes(select_lane_word(message), sizeof(T), metadata);
    }

    /**
     * @brief Push `bytes` bytes staged in shared memory
     *
     * Lane c reads payload word c (one ds_read_b64). `lds` is 8-byte aligned
     * shared memory readable for round_up(bytes, 8) bytes and published (LDS
     * barrier) before the call. Bytes of the last word past `bytes` are
     * written to the ring but never returned by pop().
     */
    __device__ bool push_shared(const void* lds, size_t bytes, uint32_t metadata = 0) {
        using LdsWord = const __attribute__((address_space(3))) uint64_t;
        return push_words((LdsWord*)lds, bytes, metadata);  // address-space cast
    }

    /**
     * @brief Push `bytes` bytes from global memory
     *
     * Same contract as push_shared() for an 8-byte aligned global pointer
     * (one global_load_dwordx2 per lane).
     */
    __device__ bool push_global(const void* ptr, size_t bytes, uint32_t metadata = 0) {
        return push_words((global_ptr<const uint64_t>)ptr, bytes, metadata);  // address-space cast
    }

private:
    __device__ Handle& handle() { return static_cast<Handle&>(*this); }

    template<typename WordPtr>
    __device__ bool push_words(WordPtr words, size_t bytes, uint32_t metadata) {
        const uint32_t n = checked_bytes(bytes);
        const size_t lane = __lane_id();
        const uint64_t word = lane * kChunkPayloadBytes < n ? words[lane] : 0;
        return handle().push_lanes(word, n, metadata);
    }
};

/**
 * @brief Cached readiness state for a device-to-host queue's host consumer
 *
 * ready() is one load of the next message's chunk-0 signal word and an epoch
 * compare; it never reads the shared consumer index. Call refresh() after
 * any operation that may have consumed messages (pop(), invoke
 * Context::progress()). ready() is a hint: it can be true while pop() still
 * returns false, when chunk 0 is visible before the message's other chunks.
 */
template<typename Queue>
class HostPollState {
public:
    __host__ bool ready() const {
        return signal_epoch(*signal_) == expected_epoch_;
    }

    /// Rebind to the queue's current consumer position.
    __host__ void refresh() {
        const uint64_t head = queue_->consumer_position();
        signal_ = reinterpret_cast<const volatile uint64_t*>(
                      queue_->chunks_ + (head & queue_->capacity_mask_) * kChunksPerLine) + 1;
        expected_epoch_ = line_epoch(head, queue_->log2_capacity_);
    }

private:
    friend Queue;

    __host__ explicit HostPollState(const Queue* queue) : queue_(queue) {
        refresh();
    }

    const Queue* queue_;
    const volatile uint64_t* signal_ = nullptr;  ///< forces a fresh coherent load
    uint8_t expected_epoch_ = 0;
};

} // namespace detail
} // namespace kiwi::queue
